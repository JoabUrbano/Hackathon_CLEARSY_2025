IMPLEMENTATION CONTROLLER_i
REFINES CONTROLLER
SEES
    CTX , SENSORS , ENABLER

INITIALISATION
    current_action := NONE ;
    current_authentication := AUTHENTICATED_NONE ;
    current_objective := OBJ_NONE

OPERATIONS
    process_readers =
    IF current_authentication = AUTHENTICATED_NONE THEN
        IF card_reader_a = TRUE THEN
            current_authentication := AUTHENTICATED_A
        ELSIF card_reader_b = TRUE THEN
            current_authentication := AUTHENTICATED_B
        ELSIF card_reader_l = TRUE THEN
            current_authentication := AUTHENTICATED_L
        END
    END ;

    control =
    BEGIN
        /* Objectives */
        IF // Check if the current action has already completed the objective
            ( current_objective = OBJ_OPEN_DOOR_A & current_action = TRANSLATE_OPEN_DOOR_A ) or
            ( current_objective = OBJ_OPEN_DOOR_B & current_action = TRANSLATE_OPEN_DOOR_B )
        THEN
            current_objective := OBJ_NONE

        ELSIF // If the authentication is A or L, check buttons in room A and from room L to A
            current_authentication = AUTHENTICATED_A or
            current_authentication = AUTHENTICATED_L
        THEN
            IF
                button_room_a_open_a = TRUE or
                button_room_l_open_a = TRUE
            THEN
                current_objective := OBJ_OPEN_DOOR_A

            END

        ELSIF // If the authentication is B or L, check buttons in room B and from room L to B
            current_authentication = AUTHENTICATED_B or
            current_authentication = AUTHENTICATED_L
        THEN
            IF
                button_room_b_open_b = TRUE or
                button_room_l_open_b = TRUE
            THEN
                current_objective := OBJ_OPEN_DOOR_B

            END
        END ;

        /* Actions */
        IF // If the objective is to open door A    
            current_objective = OBJ_OPEN_DOOR_A
        THEN
            IF // Pressure is already set to A and door B is closed: we can open door A
                pressure_sensor_l = PRESSURE_A &
                contact_sensor_b = TRUE
            THEN
                current_action := TRANSLATE_OPEN_DOOR_A

            ELSIF // If pressure is not A and both doors are closed: change pressure to A
                pressure_sensor_l /= PRESSURE_A &
                contact_sensor_a = TRUE &
                contact_sensor_b = TRUE
            THEN
                current_action := ADAPT_PRESSURE_L_TO_A

            ELSIF // If door B is open, close it first
                contact_sensor_b = FALSE
            THEN
                current_action := TRANSLATE_CLOSE_DOOR_B
            END

        ELSIF // If the objective is to open door B
            current_objective = OBJ_OPEN_DOOR_B
        THEN
            IF // Pressure is already set to B and door A is closed: we can open door B
                pressure_sensor_l = PRESSURE_B &
                contact_sensor_a = TRUE
            THEN
                current_action := TRANSLATE_OPEN_DOOR_B

            ELSIF // If pressure is not B and both doors are closed: change pressure to B
                pressure_sensor_l /= PRESSURE_B &
                contact_sensor_a = TRUE &
                contact_sensor_b = TRUE
            THEN
                current_action := ADAPT_PRESSURE_L_TO_B

            ELSIF // If door A is open, close it first
                contact_sensor_a = FALSE
            THEN
                current_action := TRANSLATE_CLOSE_DOOR_A
            END

        ELSE // If there is no objective, change the action to none
            current_action := NONE
        END
    END
END
